#!/usr/bin/perl
#
# Load data from /etc/hosttag into our 
#

use strict;

use File::Basename;
use TokyoCabinet;

my $DB = '/var/lib/misc/hosttag.tch';
my $data_dir = '/etc/hosttag';

die "Cannot find data dir '$data_dir'\n" unless -d $data_dir;
die "Hosttag database '$DB' is not writeable\n" unless -w $DB;

my %tag = ();

# Load all data
for my $host_dir (glob("$data_dir/*")) {
  next if $host_dir eq '.' || $host_dir eq '..';
  next if ! -d $host_dir;

  my $host = basename $host_dir;
  
  for my $tag_file (glob("$host_dir/*")) {
    $tag{ $host } ||= [];
    push @{ $tag{ $host } }, basename( $tag_file );
  }
}

# Invert %tag to get %host hash
my %host = ();
for my $host (keys %tag) {
  for my $tag ( @{ $tag{$host} } ) {
    $host{ $tag } ||= [];
    push @{ $host{ $tag } }, $host;
  }
}

# Write database
my $db = TokyoCabinet::HDB->new;
$db->open( $DB, $db->OWRITER | $db->OCREAT )
  or die "DB $DB open failed: " . $db->errmsg( $db->ecode );

for my $host (keys %tag) {
  $db->put("hosts/$host", join(' ', sort @{ $tag{$host} }) . "\n")
    or die "DB $DB put failed: " . $db->errmsg( $db->ecode );
}
for my $tag (keys %host) {
  $db->put("tags/$tag", join(' ', sort @{ $host{$tag} }) . "\n")
    or die "DB $DB put failed: " . $db->errmsg( $db->ecode );
}
$db->put("tags/ALL", join(' ', sort keys %tag) . "\n") if keys %tag;
$db->put("hosts/ALL", join(' ', sort keys %host) . "\n") if keys %host;

# Check for orphaned hosts/tags
$db->iterinit;
while (my $key = $db->iternext) {
  if ($key =~ m! ^hosts/(.*)$ !x) {
    my $host = $1;
    next if $host eq 'ALL';
    next if exists $tag{$host};
    print "Deleting orphaned host '$host'\n";
    $db->out($key);
  }
  elsif ($key =~ m! ^tags/(.*)$ !x) {
    my $tag = $1;
    next if $tag eq 'ALL';
    next if exists $host{$tag};
    print "Deleting orphaned tag '$tag'\n";
    $db->out($key);
  }
}

$db->close
  or die "DB $DB close failed: " . $db->errmsg( $db->ecode );

